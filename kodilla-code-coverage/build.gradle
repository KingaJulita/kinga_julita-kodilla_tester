plugins {
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id "com.github.spotbugs" version "6.0.14"
}

group = 'com.kodilla'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation group: 'com.mysql', name: 'mysql-connector-j', version: '9.0.0'
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion "8.30"
    configFile = file("checkstyle.xml")
}

spotbugs {
    ignoreFailures = true
}

spotbugsMain {
    reports {
        html {
            required = true
        }
    }
}

spotbugsTest {
    reports {
        html {
            required = true
        }
    }
}
jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            includes = ['com.kodilla.jacoco.*']

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1.0 // Wymagane 100% pokrycia decyzji
            }
        }
    }
}
